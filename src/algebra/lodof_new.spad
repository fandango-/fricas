)abbrev package LODOFN LinearOrdinaryDifferentialOperatorFactorizerNew
++ Description:
++ \spadtype{LinearOrdinaryDifferentialOperatorFactorizerNew} provides a
++ factorizer for linear ordinary differential operators whose coefficients
++ are rational functions.
++ Keywords: differential equation, ODE, LODO, factoring
LinearOrdinaryDifferentialOperatorFactorizerNew(R, var, cen) : Exports == Impl where
  R : Join(EuclideanDomain, Comparable, CharacteristicZero,
           RetractableTo Integer, LinearlyExplicitRingOver Integer)
  var : Symbol
  cen : Expression R

  F ==> Expression R
  UP ==> UnivariatePolynomial(var, F)
  RF ==> Fraction UP
  SUP ==> SparseUnivariatePolynomial(F)
  L  ==> LinearOrdinaryDifferentialOperator1 RF
  L2 ==> LinearOrdinaryDifferentialOperator2(UP, RF)
  L3 ==> LinearOrdinaryDifferentialOperator3(F, UP, RF)
  UTS ==> UnivariateTaylorSeries(F, var, cen)
  US ==> UnivariateLaurentSeries(F, var, cen)
  LL ==> LinearOrdinaryDifferentialOperator3(F, UTS, US)
  PO ==> Record(x : Integer, y : Integer)
  FZ ==> Fraction Integer
  FA ==> Record(factor : UP, exponent : Integer)
  F2 ==> Record(Qt : LL, Rt : LL)
  NL ==> Record(main : F2, nf : F2, error : LL)
  LW ==> Record(lr : NL, n_lifts : PositiveInteger)
  TERM ==> Record(k : Integer, c : F)
  LC ==> Record(llc : List Stream TERM, rlc : List Stream TERM)
  LAU ==> Record(laurl : List US, laurr : List US)
  SP ==> List(Record(op : LL, ram : UP, expart : US))
  RS ==> Union(List LL, SP)

  Exports ==> with
    testnp : L3 -> List(Record(point : PO, slope : FZ, npoly : UP))
    ++ test function.
    testfn : (L3, SUP -> Factored(SUP), String) -> List LL
    ++ test function.
    testf2 : (L3, UP, FZ) -> F2
    ++ test function.
    testll : (FZ, L3, UP, UP, FZ, Integer) -> LAU
    ++ test function.
    testlc : (FZ, L3, UP, UP, FZ, Integer) -> LC
    ++ test function.
    testln : (FZ, L3, UP, UP, FZ, Integer) -> NL
    ++ test function.
    testlw : (FZ, L3, UP, UP, FZ, Integer) -> Stream F2
    ++ test function.
    testcp : (L3, FZ, Integer) -> LL
    ++ test function.
    testco : (L3, FZ, Integer) -> Boolean
    ++ test function.
    testcx : (L3, FZ) -> UP
    ++ test function.
    testpd : UP -> LL
    ++ test function.
    testfr : (L3, SUP -> Factored(SUP), String) -> RS
    ++ test function.
    testsb : (Union(UP, US), L3) -> LL
    ++ test function.
    testro : (L3, F, PositiveInteger) -> Boolean
    ++ test function.
    testir : (L3, F, PositiveInteger) -> LL
    ++ test function.
    testfo : (L3, SUP -> Factored(SUP), String) -> RS
    ++ test function.

  Impl ==> add
    import from IntegerRetractions(F)
    import from StreamFunctions2(LW, F2)
    import from StreamFunctions2(F2, LL)
    import from StreamFunctions3(LL, Integer, TERM)

    convertUPtoUTS : UP -> UTS
    convertL3toLL : L3 -> LL
    newtonpolygonPoints : LL -> List PO
    newtonpolygon : LL -> List(Record(point : PO, slope : FZ, npoly : UP))
    factor_newton : (LL, SUP -> Factored(SUP), String) -> List LL
    factorUP : (UP, SUP -> Factored(SUP)) -> Factored(UP)
    factor_newton2 : (LL, UP, Record(point : PO, slope : FZ, npoly : UP)) -> F2
    laurent_op : (List US, Integer) -> LL
    list_laurent : (LC, Integer, Integer) -> LAU
    list_laurentop : (List Stream TERM, Integer) -> List US
    listream_coefs : (Stream F2, Integer, Integer, FZ, FZ) -> LC
    listream_coefsop : (Stream LL, Integer, Integer, FZ) -> List Stream TERM
    lift_newton : (FZ, LL, UP, UP, FZ, Integer, F2, LL, PositiveInteger) -> NL
    ln_wrapper : (FZ, LL, UP, UP, FZ, Integer) -> Stream F2
    coefs_poly : (LL, FZ, Integer) -> UP
    coefs_operator : (UP, FZ, Integer) -> LL
    coeffx : (LL, FZ) -> UP
    plug_delta : UP -> LL
    factor_riccati : (LL, SUP -> Factored(SUP), String) -> RS
    substitute : (Union(UP, US), LL) -> LL
    ramification_of : (LL, F, PositiveInteger) -> LL
    iram_of : (LL, F, PositiveInteger) -> LL
    factor_op : (LL, SUP -> Factored(SUP), String) -> RS

    convertUPtoUTS(np) ==
        tools := UTSodetools(F, UP, L2, UTS)
        UP2UTS(np)$tools

    convertL3toLL(f) ==
        tools := UTSodetools(F, UP, L2, UnivariateTaylorSeries(F, var, cen))
        apf : LinearOrdinaryDifferentialOperator3(F, UTS, US) := 0
        ftmp := f
        while ftmp ~= 0 repeat
            lc := leadingCoefficient(ftmp)
            den := UP2UTS(denom lc) :: US
            apf := (apf +
                    monomial((UP2UTS(numer lc)$tools *
                    recip(den) :: US), degree(ftmp)))
            ftmp := reductum ftmp
        apf

-- newtonpolygonPoints takes a LinearOrdinaryDifferentialOperator3 having
-- Laurent series coefficients as input and returns the coordinates of the
-- extreme points of the Newton polygon.
    newtonpolygonPoints(f) ==
        ftmp := f
        points : List PO := []
        yzero := order(leadingCoefficient(ftmp))
        while ftmp ~= 0 repeat
            xv := degree(ftmp)
            yv := order(leadingCoefficient(ftmp))
            if xv ~= 0 then
                points := cons([xv, yv], points)
            yzero := min(yzero, yv)
            ftmp := reductum ftmp
        points := cons([0, yzero], points)
        npg : List PO := [points.2, points.1]
        points3 := rest(rest(points))
        for i in points3 repeat
            ccw : Boolean := true
            while #npg > 1 and ccw repeat
                p1 := npg.2
                p2 := npg.1
                sb1 := (p2.x - p1.x) * (i.y - p1.y)
                sb2 := (p2.y - p1.y) * (i.x - p1.x)
                ccw := (sb1 - sb2) <= 0
                if ccw then
                    npg := rest(npg)
            npg := cons(i, npg)
        reverse!(npg)

--    newtonpolygonPoints(f) ==
--        n := degree(f)
--        vals := [order(coefficient(f, i), order(leadingCoefficient(f)))
--                 for i in 0..n]
--        powd : Integer := 0
--        val_powd := vals.1
--        vals1 := vals.1
--        for i in vals | i ~= vals1 repeat
--            val_powd := min(val_powd, i)
--        npg : List PO := [[powd, val_powd]]
--        while powd < n repeat
--            m := (vals(powd+2) - val_powd) :: Fraction Integer
--            for j in 2..(n - powd) repeat
--                m := min(m, (vals(powd+j+1) - val_powd) / j)
--            i : Integer := n
--            while (vals(i+1)-val_powd) :: Fraction Integer ~= (i-powd)*m repeat
--                i := i - 1
--            powd := i
--            val_powd := vals(i+1)
--            npg := append(npg, [[i, vals(i+1)]])
--        npg

-- newtonpolygon takes a LinearOrdinaryDifferentialOperator3 having Laurent
-- series coefficients as input and returns the coordinates of the extreme
-- points of the Newton polygon, the slope of a point to the next point and the
-- Newton polynomial of this slope.
    newtonpolygon(f) ==
        npg : List PO := newtonpolygonPoints(f)
        res : List(Record(point : PO, slope : FZ, npoly : UP)) := []
        restl := rest(npg)
        for i in npg for nxt in restl repeat
            -- now we compute the Newton polynomial of slope number i
            slop : FZ := (nxt.y - i.y) / (nxt.x - i.x)
            npol : UP := 0
            for j in 0..qcoerce((nxt.x - i.x) quo denom(slop)) repeat
                ind : NonNegativeInteger := qcoerce(denom(slop) * j + i.x)
                opc := coefficient(f, ind)
                lsc := coefficient(opc, j * numer(slop) + i.y)
                npol := npol + monomial(lsc, j)
            res := append(res, [[i, slop, npol]])
        res

    testnp(f) ==
        newtonpolygon(convertL3toLL(f))

-- factor_newton takes a monic LinearOrdinaryDifferentialOperator3 having
-- Laurent series coefficients, a factoring routine for polynomials and a
-- string as input and returns the coprime index 1 factorizations.
-- The possible option strings are:
-- "split over k((x))" results in a complete Newton factorization, i.e. the
-- broken Newton polygon and the gcd 1 reducible Newton polynomial cases will
-- be factored.
-- "alg factor" gives only one right factor, the one where the slope is the
-- least steep.
-- "semireg" gives a coprime index 1 LCLM factorization, to be used for
-- computing the semi-regular parts.
-- any other string as the third argument gives all possible safe right factors
-- according to the Newton method.
-- References : Section 5 of
-- http://www.math.fsu.edu/~hoeij/compalg/diffop/versie9_3.pdf
    factor_newton(f, factorizer, option) ==
        if degree(f) <= 1 then
            return [f]
        np := newtonpolygon(f)
        res : List LL := []
        unsafe : Set FA
        for k in np repeat
            npol := k.npoly
            v := factors(factorUP(npol, factorizer))
            if k.slope ~= 0 then
                v := [[il.factor ^ qcoerce(il.exponent), 0] for il in v]
            else
                -- regular singular case, we compute the unsafe factors
                unsafe := set []
                -- n_unsafe is the number of factors with an exponent that is
                -- the exponent of this factor i minus a positive integer.
                n_unsafe : Table (FA, Integer) := table()
                semi : Table(FA, UP) := table()
                for i in v repeat
                    n_unsafe(i) := 0
                    semi(i) := i.factor ^ qcoerce(i.exponent)
                for i in v for ii in 1..(#v)-1 repeat
                    restl := rest(v, ii)
                    vif : UP := i.factor
                    dvif := degree(vif)
                    for j in restl repeat
                        vjf : UP := j.factor
                        if dvif = degree(vjf) then
                            d := qcoerce(dvif - 1)
                            e : F := coefficient(vif, d) - coefficient(vjf, d)
                            e := e / ((dvif :: Integer) :: F)
                            if integer?(e) and e ~= 0 then
                                ie := integer(e)
                                id := d :: Integer
                                quot := (ie quo (id + 1)) :: UP
                                newx : UP := monomial(1,1) + quot
                                sbt := eval(vjf, monomial(1,1), newx)
                                (vif - sbt) ~= 0 => "iterate"
                                if ie > 0 then
                                    unsafe := union(unsafe, i)
                                    n_unsafe(j) := (n_unsafe(j)+i.exponent)
                                    semi(j) := (semi(j) * vif ^
                                                qcoerce(i.exponent))
                                else
                                    unsafe := union(unsafe, j)
                                    n_unsafe(i) := (n_unsafe(i)+j.exponent)
                                    semi(i) := (semi(i) * vjf ^
                                                qcoerce(j.exponent))
                v := members(difference(set(v), unsafe))
                if option = "semireg" then
                    v := [[semi(i), 1] for i in v]
            -- now v contains the safe right factors of slope number k
            slo := k.slope
            if degree(v(1).factor) * denom(slo) = degree(f) then
                -- f allows no coprime index 1 factorization
                return [f]
            for i in v repeat
                jl : F2 := factor_newton2(f, i.factor, k)
                if option = "alg factor" then
                    return [jl.Rt]
                else if option = "split over k((x))" then
                    return append(factor_newton(jl.Qt, factorizer, option),
                                  [jl.Rt])
                res := append(res, [jl.Rt])
        res

    testfn(f, factorizer, option) ==
        factor_newton(convertL3toLL(f), factorizer, option)

    factorUP(np, factorSUP) ==
        snp : SUP := makeSUP(np)
        fsnp : Factored SUP := factorSUP(snp)
        map((x : SUP) : UP +-> unmakeSUP(x), fsnp)$FactoredFunctions2(SUP, UP)

-- factor_newton2 is used by factor_newton. It factors a monic
-- LinearOrdinaryDifferentialOperator3 with Laurent series coefficients, given
-- the operator, the Newton polynomial of the right factor (which must be monic)
-- and the term of the Newton polygon to whose slope the polynomial corresponds.
-- The output of this function is a Record of 2 factors.
    factor_newton2(f, r, l) ==
        slop := l.slope
        ycd : Integer := l.point.y
        xcd : Integer := l.point.x
        npol := l.npoly
        shift := min(0, ycd :: FZ + (degree(f) - xcd) * slop)
        -- we will compute the newton polynomial of the left factor
        npo : UP := monomial(1, qcoerce(xcd))
        mlr : UP := eval((npol quo r),monomial(1,1),monomial(1,qcoerce(denom(slop))))
        npo := npo * mlr
        npor : UP := eval(r, monomial(1, 1), monomial(1, qcoerce(denom(slop))))
        ordr := degree(r) * denom(slop)
        ordl := degree(f) - ordr
        -- this number (20) has been arbitrarily chosen : will be modified soon
        -- lnr := lift_newton(slop, f, npo, npor, shift, ordr, 20)
        -- [lnr.Qt, lnr.Rt]
        lwr := ln_wrapper(slop, f, npo, npor, shift, ordr)
        lcr := listream_coefs(lwr, ordl, ordr, shift, slop)
        llr := list_laurent(lcr, ordl, ordr)
        left := laurent_op(llr.laurl, ordl)
        right := laurent_op(llr.laurr, ordr)
        [left, right]

    testf2(f, r, slop) ==
        np := newtonpolygon(convertL3toLL(f))
        l : Record(point : PO, slope : FZ, npoly : UP)
        for i in np repeat
            if i.slope = slop then
                l := i
        factor_newton2(convertL3toLL(f), r, l)

-- laurent_op takes a list of Laurent series which are the coefficients of an
-- operator f of the given degree (the second input) in order and produces f.
    laurent_op(llaur, dgf) ==
        f : LL := 0
        for i in 0..dgf repeat
            f := f + monomial(llaur.(i + 1), i)
        f

-- list_laurent takes a pair of lists of streams of coefficients of a Laurent
-- series in the form required by the series function and produces the
-- corresponding pair of Laurent series. The number of terms in the 2 lists are
-- given by dgl+1 and dgr+1 respectively.
    list_laurent(lsc, dgl, dgr) ==
        laurel := list_laurentop(lsc.llc, dgl)
        laurer := list_laurentop(lsc.rlc, dgr)
        [laurel, laurer]

    testll(slop, f, l_low, r_low, shift, dgr) ==
        lwr := ln_wrapper(slop, convertL3toLL(f), l_low, r_low, shift, dgr)
        lcr := listream_coefs(lwr, degree(f) - dgr, dgr, shift, slop)
        list_laurent(lcr, degree(f) - dgr, dgr)

-- list_laurentop takes a list of stream of coefficients of a Laurent series in
-- the form required by the series function and produces the corresponding
-- Laurent series. The number of terms in the list is given by dgf+1.
    list_laurentop(lsc, dgf) ==
        res : List US := []
        for i in 0..dgf repeat
            res := append(res, [series(lsc.(i + 1))])
        res

-- listream_coefs takes a stream of pairs of operators [l_extra, r_extra] and
-- returns a pair of lists of streams of coefficients in the form required by
-- the series function (i.e. of type Stream TERM where TERM is as defined). The
-- number of terms in the 2 lists is given by dgl+1 and dgr+1 respectively.
    listream_coefs(slr, dgl, dgr, shift, slop) ==
        sle := map((landr : F2) : LL +-> landr.Qt, slr)
        le := denom(slop) * retract(shift)@Integer
        le := le - numer(slop) * dgl
        lsc := listream_coefsop(sle, dgl, le, slop)
        sre := map((landr : F2) : LL +-> landr.Rt, slr)
        re := -(numer(slop) * dgr)
        rsc := listream_coefsop(sre, dgr, re, slop)
        [lsc, rsc]

    testlc(slop, f, l_low, r_low, shift, dgr) ==
        lwr := ln_wrapper(slop, convertL3toLL(f), l_low, r_low, shift, dgr)
        listream_coefs(lwr, degree(f) - dgr, dgr, shift, slop)

-- listream_coefsop takes a stream of operators and returns a list of streams
-- of coefficients in the form required by the series function (i.e. of type
-- Stream TERM where TERM is as defined). The number of terms in the list is
-- given by dgf+1.
    listream_coefsop(sfe, dgf, fe, slop) ==
        res : List Stream TERM := []
        for i in 0..dgf-1 repeat
            nl_st : Stream Integer := stream((x : Integer) : Integer +-> x + 1, 1)
            new_term := map((x : LL, n_l : Integer) : TERM +-> (
                            degn := (n_l-1)*denom(slop) + fe + i * numer(slop);
                            deg : Union(Integer, "failed");
                            deg := retractIfCan(degn /$FZ denom(slop));
                            while deg = "failed" repeat
                                (degn := degn + 1;
                                deg := retractIfCan(degn /$FZ denom(slop)););
                            cx := coefficient(x, i);-- extract coefficient of op
                            coefd := coefficient(cx, deg :: Integer);
                            [deg :: Integer, coefd]), sfe, nl_st)
            res := append(res, [new_term])
        dgc := [[[0$Integer, 1$F]] :: Stream TERM]
        append(res, dgc)

-- lift_newton is a procedure that lifts "coprime index 1 factorizations".
-- Meaning of the variables:
-- slop : the concerned slope of the Newton polygon.
-- ff, li, ri : exact operators ff = li * ri is the final goal.
-- l_low, r_low : the lowest line of li and ri. These have gcd 1.
-- shift : the shift of left term.
-- dgr : the degree of ri / v.Rt
-- v : the pair of operators li, ri
-- ei : the error term = li * ri - ff
-- n_l : the lift number that is n_l-1 gives number of lifts performed so far.
-- l_extra, r_extra : the new terms we computed of li and ri.
-- References : Section 4 of
-- http://www.math.fsu.edu/~hoeij/compalg/diffop/versie9_3.pdf
    lift_newton(slop, ff, l_low, r_low, shift, dgr, v, ei, n_l) ==
        dgl := degree(ff) - dgr
        li : LL := v.Qt
        ri : LL := v.Rt
        if slop = 0 then
            -- regular singular case
            i := n_l-1
            r_extra : LL
            l_extra : LL
            if i = 0 then
                r_extra := plug_delta(r_low)
                l_extra := (plug_delta(l_low) * monomial(1,
                                                retract(shift)@Integer)$US)
            else
                sl_low := eval(l_low, monomial(1,1), monomial(1,1)+i :: UP)
                s := extendedEuclidean(r_low, sl_low)
                if s.generator ~= 1 then
                    error "unsafe factor"
                c1 := s.coef1
                c2 := s.coef2
                pi := coeffx(ei, i :: FZ + shift)
                pr_extra := -((c2 * pi) rem r_low)
                r_extra := plug_delta(pr_extra)
                pl_extra := (pi + pr_extra * sl_low) quo r_low
                l_extra := plug_delta(pl_extra)
                l_extra := -(monomial(1, i + retract(shift)@Integer)$US *
                             l_extra)
                r_extra := monomial(1, i)$US * r_extra
            li := li + l_extra
            ei := ei + l_extra * ri + li * r_extra
            ri := ri + r_extra
        else
            s := extendedEuclidean(r_low, l_low, 1) :: Record(coef1 : UP, coef2 : UP)
            c1 := s.coef1
            c2 := s.coef2
            le : Integer := denom(slop) * retract(shift)@Integer
            le := le - numer(slop) * dgl
            re : Integer := -(numer(slop) * dgr)
            fe : Integer := denom(slop) * retract(shift)@Integer
            fe := fe - numer(slop) * degree(ff)
            for i in (n_l-1)*denom(slop)..n_l*denom(slop)-1 repeat
                r_extra : LL
                l_extra : LL
                if i = 0 then
                    r_extra := coefs_operator(r_low, slop, re)
                    l_extra := coefs_operator(l_low, slop, le)
                else
                    pi := coefs_poly(ei, slop, i + fe)
                    pr_extra := (c2 * pi) rem r_low
                    pl_extra := pi - pr_extra * l_low
                    pl_extra := -(pl_extra quo r_low)
                    l_extra := coefs_operator(pl_extra, slop, i + le)
                    r_extra := coefs_operator(-(pr_extra), slop, i + re)
                li := li + l_extra
                ei := ei + l_extra * ri + li * r_extra
                ri := ri + r_extra
        [[li - v.Qt, ri - v.Rt], [li, ri], ei]

    testln(slop, f, l_low, r_low, shift, dgr) ==
        v : F2 := [0, 0]
        ei := -convertL3toLL(f)
        for i in 1..5 repeat
            lnr := lift_newton(slop, convertL3toLL(f), l_low, r_low, shift,
                               dgr, v, ei, qcoerce(i))
            print(lnr.main.Qt :: OutputForm)
            print(lnr.main.Rt :: OutputForm)
            v := lnr.nf
            ei := lnr.error
        lnr

-- ln_wrapper is a wrapper function for lift_newton and is used by
-- factor_newton2. It generates a stream of the extra terms (l_extra, r_extra)
-- that are generated by repeatedly applying lift_newton on its inputs.
    ln_wrapper(slop, f, l_low, r_low, shift, dgr) ==
        lni : NL := [[0, 0], [0, 0], -f]
        n_li : PositiveInteger := 1
        lni := lift_newton(slop, f, l_low, r_low, shift, dgr, lni.nf,
                           lni.error, n_li)
        all : Stream LW := stream((rec : LW) : LW +-> (
        lnr := rec.lr;
        n_l := rec.n_lifts + 1;
        lnr := lift_newton(slop, f, l_low, r_low, shift, dgr, lnr.nf,
                           lnr.error, n_l);
        [lnr, n_l]), [lni, n_li])
        res := map((rec : LW) : F2 +-> rec.lr.main, all)
        res

    testlw(slop, f, l_low, r_low, shift, dgr) ==
        ln_wrapper(slop, convertL3toLL(f), l_low, r_low, shift, dgr)

-- coefs_poly takes an operator f as input, takes coefficients based on slop and
-- i (extracts part of given valuation from f) and gives them as a polynomial.
-- slop must be > 0.
    coefs_poly(f, slop, i) ==
        nsi := extendedEuclidean(numer(slop), denom(slop)).coef1
        start_D := positiveRemainder(-i * nsi, denom(slop))
        start_x := start_D * slop + i /$FZ denom(slop)
        res : UP := 0
        for j in 0..floor(degree(f) /$FZ denom(slop)) repeat
            fc := coeffx(f, start_x + numer(slop) * j :: FZ)
            npc := coefficient(fc, qcoerce(start_D + j * denom(slop)))
            res := res + monomial(npc, qcoerce(start_D + j * denom(slop)))
        res

    testcp(f, slop, i) ==
        coefs_operator(coefs_poly(convertL3toLL(f), slop, i), slop, i)

-- coefs_operator constructs an operator form a given set of coefficients (given
-- as a polynomial np) based on slop and i which together determine valuation.
-- slop must be > 0.
    coefs_operator(np, slop, i) ==
        nsi := extendedEuclidean(numer(slop), denom(slop)).coef1
        start_D := positiveRemainder(-i * nsi, denom(slop))
        start_x := retract(start_D * slop + i /$FZ denom(slop))@Integer
        res : LL := 0
        for j in 0..ceiling(degree(np) /$FZ denom(slop)) repeat
            npc := coefficient(np, qcoerce(start_D + j * denom(slop)))
            sc := monomial(npc, start_x + numer(slop) * j)
            res := res + monomial(sc, qcoerce(start_D + j * denom(slop)))
        res

    testco(f, slop, i) ==
        np := coefs_poly(convertL3toLL(f), slop, i)
        coefs_poly(coefs_operator(np, slop, i), slop, i) = np

-- coeffx takes an operator f and an exponent e as input and returns the
-- coefficient of x^e in f while also replacing xD with x, so that the final
-- result is a polynomial.
    coeffx(f, e) ==
        ftmp := f
        ex := retract(e)@Integer
        res : UP := 0
        while ftmp ~= 0 repeat
            lc := leadingCoefficient(ftmp)
            dg := degree(ftmp)
            res := res + monomial(coefficient(lc, ex), dg)
            ftmp := reductum ftmp
        res

    testcx(f, e) ==
        coeffx(convertL3toLL(f), e)

-- plug_delta takes a polynomial as input and returns the operator obtained by
-- replacing x with xD in it.
    plug_delta(np) ==
        f : LL := 0
        nptmp := np
        while nptmp ~= 0 repeat
            lc := leadingCoefficient(nptmp)
            dg := degree(nptmp)
            f := f + monomial(lc :: US, dg)
            nptmp := reductum nptmp
        f

    testpd(np) ==
        plug_delta(np)

    factor_riccati(f, factorizer, option) ==
        np := newtonpolygon(f)
        np1 := np.1
        slop := np1.slope
        if option = "semireg" and slop = 0 then
            v := value(factor_op(f, factorizer, "alg factor") :: List LL)
            xx : UP := monomial(1, 1)
            i : UP := xx - coeffx(v, 0)
            n : Integer := degree(f)
            if n = 1 then
                il := convertUPtoUTS(i) :: US
                return [[v + il :: LL, xx, il]]
            npf := factors(factorUP(eval(np1.npoly, monomial(1, 1),
                                           monomial(1, 1) + i), factorizer))
            r : UP := 1
            sols : List Integer := []
            for l in npf repeat
                diff : Union(Integer, "failed")
                diff := retractIfCan(xx - l.factor)
                if diff ~= "failed" then
                    fact : UP := l.factor
                    exp : Integer := l.exponent
                    r := r * fact ^ qcoerce(exp)
                    sols := cons(diff :: Integer, sols)
            if degree(r) ~= n then
                error "bug"
            n := reduce(min, sols)
            r := eval(r, monomial(1, 1), monomial(1, 1) + n :: UP)
            subs := substitute(i + n :: UP, f)
            np := newtonpolygon(subs)
            lns : Record(point : PO, slope : FZ, npoly : UP)
            for ip in np repeat
                if ip.slope = 0 then
                    lns := ip
            f2r := factor_newton2(subs, r, lns).Rt
            return [[f2r, xx, convertUPtoUTS(i + n :: UP) :: US]]
        npf := factors(factorUP(np1.npoly, factorizer)).1
        if (degree(f) <= 1 or (npf.exponent = 1 and
           (option = "split over k((x))" or option = "all right factors"))) then
            if option = "semireg" then
                -- degree(f) = 1 and slope ~= 0
                il : US := truncate(coefficient(f, 0), 0)
                return [[f + il :: LL, monomial(1, 1), il]]
            else
                -- f is irreducible
                return [f]
        else if degree(npf.factor) = 1 and denom(slop) = 1 then
            -- now slope ~= 0
            cxs : US := coefficient(npf.factor, 0) * monomial(1, -numer(slop))
            -- apply a homomorphism xD -> xD + cxs to simplify the problem.
            v := factor_op(substitute(cxs, f), factorizer, option)
            if option = "semireg" then
                vsp := v :: SP
                sym : Symbol := variable(cxs)
                retv : SP := []
                for vi in vsp repeat
                    xsubsr := (convertUPtoUTS(vi.ram) :: US) ^ (-numer(slop))
                    fullxr := degree(vi.ram)*coefficient(npf.factor, 0)*xsubsr
                    retv := append(retv, [[vi.op, vi.ram, vi.expart + fullxr]])
                return retv
            vrs := v :: List LL
            return [substitute(-cxs, i) for i in vrs] :: List LL
        else if degree(npf.factor) > 1 then
            -- now we need an algebraic extension on k
            gr : F := zeroOf(makeSUP(npf.factor))
            npgr : UP := (monomial(1, 1) - gr :: UP) ^ qcoerce(npf.exponent)
            f2r := factor_newton2(f, npgr, np1)
            res := factor_riccati(f2r.Rt, factorizer, option)
            return res
        else
            -- now we need a ramification
            n := extendedEuclidean(numer(slop), denom(slop)).coef1
            coefr := coefficient(npf.factor, 0)
            ror : LL := ramification_of(f, coefr ^ n, qcoerce(denom(slop)))
            exp := retract((1 - n * numer(slop)) /$FZ denom(slop))@Integer
            tosub : F := denom(slop) * coefr ^ exp
            r : UP := (monomial(1, 1) - tosub :: UP) ^ qcoerce(npf.exponent)
            np := newtonpolygon(ror)
            lns : Record(point : PO, slope : FZ, npoly : UP)
            for i in np repeat
                if i.slope = (numer(slop) :: FZ) then
                    lns := i
            f2r := factor_newton2(ror, r, lns)
            v := factor_riccati(f2r.Rt, factorizer, option)
            res : RS
            if v case SP then
                vsp := v :: SP
                res := [[i.op, eval(r, monomial(1, 1), i.ram), i.expart]
                        for i in vsp] :: SP
            else
                vrs := v :: List LL
                res := [iram_of(i, coefr ^ n, qcoerce(denom(slop)))
                        for i in vrs] :: List LL
            return res

    testfr(f, factorizer, option) ==
        factor_riccati(convertL3toLL(f), factorizer, option)

    substitute(a, f) ==
        new_mon : LL := monomial(1, 1)
        if a case UP then
            new_mon := new_mon + convertL3toLL(a :: UP :: RF :: L3)
        else
            new_mon := new_mon + a :: US :: LL
        ftmp := f
        res : LL := 0
        while ftmp ~= 0 repeat
            lc := leadingCoefficient(ftmp)
            dg := degree(ftmp)
            res := res + lc * new_mon ^ dg
            ftmp := reductum ftmp
        res

    testsb(a, f) ==
        substitute(a, convertL3toLL(f))

    ramification_of(f, coefr, n) ==
        if coefr = 1 and n = 1 then
            return f
        res : LL := 0
        ftmp : LL := f
        while ftmp ~= 0 repeat
            lc := leadingCoefficient(ftmp)
            dg := degree(ftmp)
            mlc := multiplyCoefficients((x : Integer) : F +-> coefr ^ x, lc)
            mlc := multiplyExponents(mlc, n)
            extra := monomial(mlc, dg) * ((1 /$FZ n) ^ dg) :: F :: US
            res := res + extra
            ftmp := reductum ftmp
        n ^ degree(f) * res

    testro(f, coefr, n) ==
        ror := ramification_of(convertL3toLL(f), coefr, n)
        iram_of(ror, coefr, n) = convertL3toLL(f)

    iram_of(f, coefr, n) ==
        if coefr = 1 and n = 1 then
            return f
        res : LL := 0
        ftmp : LL := f
        while ftmp ~= 0 repeat
            lc := leadingCoefficient(ftmp)
            dg := degree(ftmp)
            tlc := terms(lc)
            tlc := map((x : TERM) : TERM +-> (
            ndeg := x.k quo n;
            ncoef := x.c / (coefr ^ ndeg);
            [ndeg, ncoef]), tlc)
            mlc := series(tlc)
            extra := monomial(mlc, dg) * (n ^ dg) :: US
            res := res + extra
            ftmp := reductum ftmp
        res * ((1 /$FZ n) ^ degree(f)) :: F :: US

    testir(f, coefr, n) ==
        iram_of(convertL3toLL(f), coefr, n)

    factor_op(f, factorizer, option) ==
        fnr := factor_newton(f, factorizer, option)
        if option = "semireg" then
            res : SP := []
            for i in fnr repeat
                res := append(res,
                              factor_riccati(i, factorizer, option) :: SP)
            return res
        else
            res : List LL := []
            for i in fnr repeat
                res := append(res,
                              factor_riccati(i, factorizer, option) :: List LL)
            return res

    testfo(f, factorizer, option) ==
        factor_op(convertL3toLL(f), factorizer, option)

--Copyright (c) 2015, Abhinav Baid.
--All rights reserved.
--
--Redistribution and use in source and binary forms, with or without
--modification, are permitted provided that the following conditions are
--met:
--
--    - Redistributions of source code must retain the above copyright
--      notice, this list of conditions and the following disclaimer.
--
--    - Redistributions in binary form must reproduce the above copyright
--      notice, this list of conditions and the following disclaimer in
--      the documentation and/or other materials provided with the
--      distribution.
--
--    - Neither the name of The Numerical ALgorithms Group Ltd. nor the
--      names of its contributors may be used to endorse or promote products
--      derived from this software without specific prior written permission.
--
--THIS SOFTWARE IS PROVIDED BY THE COPYRIGHT HOLDERS AND CONTRIBUTORS "AS
--IS" AND ANY EXPRESS OR IMPLIED WARRANTIES, INCLUDING, BUT NOT LIMITED
--TO, THE IMPLIED WARRANTIES OF MERCHANTABILITY AND FITNESS FOR A
--PARTICULAR PURPOSE ARE DISCLAIMED. IN NO EVENT SHALL THE COPYRIGHT OWNER
--OR CONTRIBUTORS BE LIABLE FOR ANY DIRECT, INDIRECT, INCIDENTAL, SPECIAL,
--EXEMPLARY, OR CONSEQUENTIAL DAMAGES (INCLUDING, BUT NOT LIMITED TO,
--PROCUREMENT OF SUBSTITUTE GOODS OR SERVICES; LOSS OF USE, DATA, OR
--PROFITS; OR BUSINESS INTERRUPTION) HOWEVER CAUSED AND ON ANY THEORY OF
--LIABILITY, WHETHER IN CONTRACT, STRICT LIABILITY, OR TORT (INCLUDING
--NEGLIGENCE OR OTHERWISE) ARISING IN ANY WAY OUT OF THE USE OF THIS
--SOFTWARE, EVEN IF ADVISED OF THE POSSIBILITY OF SUCH DAMAGE.

-- Compile order for the differential equation solver:
-- oderf.spad  odealg.spad  nlode.spad  nlinsol.spad  riccati.spad
-- kovacic.spad  lodof.spad  odeef.spad
